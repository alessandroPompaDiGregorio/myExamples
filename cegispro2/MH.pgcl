# python3 -m cegispro2.cmd MH.pgcl --post "[win=1] + [not (win=1)]*0" --prop "[flag=0]*0.671"
# python3 -m cegispro2.cmd MH.pgcl --post "[win=1] + [not (win=1)]*0" --prop "[flag=0]*0.661" --invarianttype sub --cdb

nat carDoor [1,3];
nat chosenDoor [1,3];
nat openedDoor [0,4];
nat win [0,1];
nat flag [0,1];

while (flag=0) {
	flag:=1;
	carDoor := 1 : 1/3 + 2 : 1/3 + 3 : 1/3;
	chosenDoor := 1 : 1/3 + 2 : 1/3 + 3 : 1/3;
	if (carDoor = chosenDoor) {
		{openedDoor:=chosenDoor-1} [0.5] {openedDoor:=chosenDoor+1}
		if (openedDoor=0) {
			openedDoor:=3;
		} else { skip }
		if (openedDoor=4) {
			openedDoor:=1;
		} else { skip }
	} else {
		openedDoor:=6-(chosenDoor+carDoor);
	}
	win:=0;
	if ((not (1=chosenDoor)) & (not (1=openedDoor)) & 1=carDoor) {
		win:=1;
	} else { skip }
	if ((not (2=chosenDoor)) & (not (2=openedDoor)) & 2=carDoor) {
		win:=1;
	} else { skip }
	if ((not (3=chosenDoor)) & (not (3=openedDoor)) & 3=carDoor) {
		win:=1;
	} else { skip }
}
